plugins {
    id 'fabric-loom' version '1.14-SNAPSHOT'
    id 'maven-publish'
}

version = "${project.mod_version}+${stonecutter.current.version}"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

repositories {
    //ModMenu
    maven { url "https://maven.terraformersmc.com/releases/" }
    //Cloth Config
    maven { url "https://maven.shedaniel.me/" }
}

dependencies {
    minecraft "com.mojang:minecraft:${stonecutter.current.project}"
    mappings "net.fabricmc:yarn:${property("yarn_mappings")}:v2"
    modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${property("fabric_version")}"

    //ModMenu
    modApi "com.terraformersmc:modmenu:${property("modmenu_version")}"

    //Cloth Config
    modApi("me.shedaniel.cloth:cloth-config-fabric:${property("clothconfig_version")}") {
        exclude(group: "net.fabricmc.fabric-api")
    }

    //Renderer
    include modImplementation("io.github.0x3c50.renderer:renderer-fabric:${property("renderer_version")}")

    //Toml
    modImplementation("com.moandjiezana.toml:toml4j:${project.toml4j_version}")
    include "com.moandjiezana.toml:toml4j:${project.toml4j_version}"
}

processResources {
    inputs.property "version", project.version
    inputs.property "minecraft", stonecutter.current.version

    filesMatching("fabric.mod.json") {
        expand "version": inputs.properties.version,
                "minecraft_version": stonecutter.current.version,
                "loader_version": project.loader_version
    }
}

loom {
    runConfigs.all {
        ideConfigGenerated(true) // Run configurations are not created for subprojects by default
        runDir = "../../run" // Use a shared run folder and create separate worlds
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.release = 21
}

java {
    withSourcesJar()

    def java = stonecutter.eval(stonecutter.current.version, ">=1.20.5")
            ? JavaVersion.VERSION_21 : JavaVersion.VERSION_17
    targetCompatibility = java
    sourceCompatibility = java
}

jar {
    inputs.property "archivesName", project.base.archivesName

    from("LICENSE") {
        rename { "${it}_${inputs.properties.archivesName}" }
    }
}

publishing {
    publications {
        create("mavenJava", MavenPublication) {
            artifactId = project.archives_base_name
            from components.java
        }
    }

    repositories {
    }
}